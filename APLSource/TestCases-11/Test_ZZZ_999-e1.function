 R←Test_ZZZ_999(stopFlag batchFlag);v;n;d;xml;rf;f1;f2;f3;f4;buffer;is;shouldBe
⍝ Check the "Version" function and Files\publish.config and Files\history.txt
 R←∆Failed

 rf←#.Markdown2HTML

⍝ First we check whether "Version" returns a valid result:
 (n v d)←rf.Version
 f1←n≢{⍵↓⍨2×'#.'≡2⍴⍵}⍕rf
 f1∨←~5≤⍴v
 f1∨←2≠'.'+.=v
 f1∨←~∧/⎕D∊⍨v~'.'

 f1∨←10≠⍴d
 f1∨←d[4 7]∨.≠'-'
 f1∨←~∧/⎕D∊⍨d~'-'

⍝ publish.config must be in line with what "Version" returns of course:
 xml←⎕XML'flat'#.APLTreeUtils.ReadUtf8File'Files\publish.config'
 f2←v≢2⊃xml[xml[;1]⍳⊂'version';]
 f2∨←d≢2⊃xml[xml[;1]⍳⊂'date';]

⍝ history.txt might be useful during development but must not exist
⍝ when the project is checked in:
 f3←#.FilesAndDirs.Exists'Files\history.txt'

 :If f1
     ⎕←'Result of the "Version" function is invalid.'
 :EndIf
 :If f2
     ⎕←'The contents ob Files\publish.config is not in line with "Version".'
 :EndIf
 :If f3
     ⎕←'There is a file Files\history.txt but it shouldn''t.'
 :EndIf

 →FailsIf f1∨f2∨f3

 R←∆OK
